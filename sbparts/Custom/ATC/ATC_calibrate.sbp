'Routine to calibrate tool clip positions for ATC
'10/15/05 th created original Center_in_XY routine
'03/31/06 BM, BY, GB modified for ATC from center in hole routine
'01/07/10 GB Change tool clip height calculation and simplify process. Changed tool clip height calculation
'           to use tool holders to determine height. Automate finding the tool holders using the touch 
'           stylus, requiring the user to only move the input clip.
'05/07/10 GAB Fix to calibrate in either Inch or Metric mode
'03/22/11 GAB Change file write so that it happens after all of the calibration is done
'           This is necessary to prevent the settings file from being left blank in the event the 
'           calibration is closed before finishing.
'11/30/11 GAB, GC Changed message for next clip and truncated text. Changed location of speed reduction to
'           occur earlier and change Jog to next toolholder to a Move. Initiated &numclips variable to "0"
'           and added a test for nil number of clips. And, added SF,0 to turn off file limit checking.
'5/5/17 DJP, Commented out dust skirt actuation on input 3
'6/20/17 GAB - Added test for PRS3 ATC and Open/Close dust skirt if not PRS4
'
'USER >>> 	Put the calibration stylus in the collet of the spindle (making sure rod is grounded)
' and bit holders in each of the clip positins so that they are upside-down from their normal position...
' ...with the threaded part facing up and the part that's held in the spindle facing down. 
'
    SL  ' let's clean out the user variables, just to be safe

' these values are used if you are using inch units...change them if you're using metric
&first = 1

if %(25) = 1 then GoTo METRICUNITS
	    &ATC_holderHeight = 2.037
        &holder_radius = .75
        &stylus_dia = .25
        &safe_lift = 0.5 
        &newspeed_XY = .4
        &newspeed_Z = .4
		&tableLength = 0
		&zBigMove = 16
		&zProxClearance = .5
		&yInitOffset = 1.5
		&clipHeightClearance = 2
		&safeEdge = .25
		&nextY = 12
    GoTo INITVARS
METRICUNITS:
        &ATC_holderHeight = 51.7398
        &holder_radius = 19.05
        &stylus_dia = 6.35
        &safe_lift = 12.7 
        &newspeed_XY = 10.16
        &newspeed_Z = 10.16
   	    &tableLength = 0
		&zBigMove = 400
		&zProxClearance = 12
		&yInitOffset = 38
		&clipHeightClearance = 50
		&safeEdge = 6
		&nextY = 305
'Initialize Variables
INITVARS:
    &startspeed_XY = %(71)
    &startspeed_Z = %(73)
    &leftstart = 0
    &rightstart = 0
    &bottomstart = 0
    &topstart = 0
    &Z_top_surface = 0
    &stylus_offset = 0
    &this_tool = 1
    &number_clips = 0
    &max_center_move = &holder_radius * 1.5
    &X_Prox = 2   'Input number prox switch is connected to
    &Y_Prox = 3   'Input number prox switch is connected to
    &Z_Prox = 5   'Input number prox switch is connected to

'Define the state of prox switch to test for when triggered by target
    &X_State = 0  '1 when Normally Open: 0 = Normally Closed
    &Y_State = 0  '1 when Normally Open: 0 = Normally Closed
    &Z_State = 0  '1 when Normally Open: 0 = Normally Closed

'Read existing Safe Z & Y values from ATC_Variables file
    C#, 89    'Read file path variables
    
    INPUT "How many ATC tool holders need to be calibrated?" &number_clips 
	if &number_clips = " " then end

    &clipstring = "&" & "NUMCLIPS = " & &number_clips 
    
START_TEST:
 	MSGBOX(Has X and Y been Zeroed? ,YesNo,Zeroed XY?)
	if &msganswer = YES then GoTo READY_TO_START
	if &msganswer = NO then GoTo HOME_TOOL
	GoTo CLEANUP

HOME_TOOL:
	CN,3
	'MX,&tableLength
' zeroing the axis and resetting table base coordinates

READY_TO_START:
    ' Moving the Z-axis up to set the absolute Z-Zero position 
    VN,0
	SF,0
    ON INP(&Z_Prox,&Z_State) GoSub ZEROZ1
      JZ, &zBigMove
    ON INP(&Z_Prox,&Z_State) GoSub ZEROZ1
      MZ, &zBigMove
    VN,1

    If &ATC = 1 Then SO,3,1    'Open dust skirt
      
	'Insert an empty tool holder in the spindle (no collet or nut)...
    PAUSE
    '...Insert an empty tool holder upside down in the #1 clip...
    PAUSE 
    '...Use the keypad move the spindle to closely centered it's tool holder over the tool holder in clip #1. Press ESC when finished with keypad.
    PAUSE

    SK

    'Place the grounding clip on the side of the tool holder in the spindle...
	PAUSE
	'...and the calibration clip on the pull stud on tool holder #1. Click OK when ready to proceed!
	PAUSE
 
    &initX = %(1)
	&initY = %(2)
	&nextHolderY = %(2) + &nextY

'Calculate the Z height of the clip
    MX, %(1)-&zProxClearance

    MS,&newspeed_XY,&newspeed_Z

 	ON INPUT(1,1) GoSub Set_ClipHeight
	MZ, -&zBigMove

    J2, &initX, &initY - &yInitOffset

    ' Insert a Drill rod into collet and tighten into spindle tool holder...
    PAUSE
    ' Make sure grounding clip is on drill rod and calibration clip is on bottom of tool holder #1.
    PAUSE

	'Use the keypad to lower the stylus tip approx. 1/4" below the top edge of the tool holder in clip #1. Press ESC when finished with keypad.
    PAUSE

	SK

NEXT_HOLDER:
	
    GoSub FIND_HOLDER
	'Z_clipbase is the top of the tool flip
	if &first = 1 then GOSUB SET_STYLUS_OFFSET
    &thisZ = &Z_top_surface + &stylus_offset
		 
    if &this_tool = 1  then GoTo TOOL01
    if &this_tool = 2  then GoTo TOOL02
	if &this_tool = 3  then GoTo TOOL03
    if &this_tool = 4  then GoTo TOOL04
    if &this_tool = 5  then GoTo TOOL05
    if &this_tool = 6  then GoTo TOOL06
    if &this_tool = 7  then GoTo TOOL07
    if &this_tool = 8  then GoTo TOOL08
    if &this_tool = 9  then GoTo TOOL09
    if &this_tool = 10 then GoTo TOOL10
    if &this_tool = 11 then GoTo TOOL11
    if &this_tool = 12 then GoTo TOOL12

TOOL01:
    &X01name = &center_X
    &Y01name = &center_Y
    &Z01name = &thisZ
	&this_tool = 2
    GoTo TOOL_DONE
TOOL02:
    &X02name = &center_X
    &Y02name = &center_Y
    &Z02name = &thisZ
	&this_tool = 3
    GoTo TOOL_DONE
TOOL03:
    &X03name = &center_X
    &Y03name = &center_Y
    &Z03name = &thisZ
	&this_tool = 4
    GoTo TOOL_DONE
TOOL04:
    &X04name = &center_X
    &Y04name = &center_Y
    &Z04name = &thisZ
	&this_tool = 5
    GoTo TOOL_DONE
TOOL05:
    &X05name = &center_X
    &Y05name = &center_Y
    &Z05name = &thisZ
	&this_tool = 6
    GoTo TOOL_DONE
TOOL06:
    &X06name = &center_X
    &Y06name = &center_Y
    &Z06name = &thisZ
	&this_tool = 7
    GoTo TOOL_DONE
TOOL07:
    &X07name = &center_X
    &Y07name = &center_Y
    &Z07name = &thisZ
	&this_tool = 8
    GoTo TOOL_DONE
TOOL08:
    &X08name = &center_X
    &Y08name = &center_Y
    &Z08name = &thisZ
	&this_tool = 9
    GoTo TOOL_DONE
TOOL09:
    &X09name = &center_X
    &Y09name = &center_Y
    &Z09name = &thisZ
	&this_tool = 10
    GoTo TOOL_DONE
TOOL10:
    &X10name = &center_X
    &Y10name = &center_Y
    &Z10name = &thisZ
	&this_tool = 11
    GoTo TOOL_DONE
TOOL11:
    &X11name = &center_X
    &Y11name = &center_Y
    &Z11name = &thisZ
	&this_tool = 12
    GoTo TOOL_DONE
TOOL12:
    &X12name = &center_X
    &Y12name = &center_Y
    &Z12name = &thisZ
	&this_tool = 13

TOOL_DONE:

    if &this_tool > &Number_Clips then GoTo CLEANUP
	
	' OK, we're ready to calibrate the next holder. Put an upside-down tool holder in next location and connect calibration clip to it and grounding clip to drill rod.
    PAUSE
    
 GoTo NEXT_HOLDER

CLEANUP:
    If &ATC = 1 Then SO,3,0

	VS, &startspeed_XY, &startspeed_Z
	
	OPEN &ATC_Variables for OUTPUT AS #1 

    WRITE #1; &clipstring

    WRITE #1;"&CLIP1_X = ";&X01name
	WRITE #1;"&CLIP1_Y = ";&Y01name
    WRITE #1;"&CLIP1_Z = ";&Z01name
  if &number_clips = 1 then GoTo WRAPUP
    WRITE #1;"&CLIP2_X = ";&X02name
    WRITE #1;"&CLIP2_Y = ";&Y02name
    WRITE #1;"&CLIP2_Z = ";&Z02name
  if &Number_Clips = 2 then GoTo WRAPUP
    WRITE #1;"&CLIP3_X = ";&X03name
    WRITE #1;"&CLIP3_Y = ";&Y03name
    WRITE #1;"&CLIP3_Z = ";&Z03name
  if &Number_Clips = 3 then GoTo WRAPUP
    WRITE #1;"&CLIP4_X = ";&X04name
    WRITE #1;"&CLIP4_Y = ";&Y04name
    WRITE #1;"&CLIP4_Z = ";&Z04name
  if &Number_Clips = 4 then GoTo WRAPUP
    WRITE #1;"&CLIP5_X = ";&X05name
    WRITE #1;"&CLIP5_Y = ";&Y05name
    WRITE #1;"&CLIP5_Z = ";&Z05name
  if &Number_Clips = 5 then GoTo WRAPUP
    WRITE #1;"&CLIP6_X = ";&X06name
    WRITE #1;"&CLIP6_Y = ";&Y06name
    WRITE #1;"&CLIP6_Z = ";&Z06name
  if &Number_Clips = 6 then GoTo WRAPUP
    WRITE #1;"&CLIP7_X = ";&X07name
    WRITE #1;"&CLIP7_Y = ";&Y07name
    WRITE #1;"&CLIP7_Z = ";&Z07name
  if &Number_Clips = 7 then GoTo WRAPUP
    WRITE #1;"&CLIP8_X = ";&X08name
    WRITE #1;"&CLIP8_Y = ";&Y08name
    WRITE #1;"&CLIP8_Z = ";&Z08name
  if &Number_Clips = 8 then GoTo WRAPUP
    WRITE #1;"&CLIP9_X = ";&X09name
    WRITE #1;"&CLIP9_Y = ";&Y09name
    WRITE #1;"&CLIP9_Z = ";&Z09name
  if &Number_Clips = 9 then GoTo WRAPUP
    WRITE #1;"&CLIP10_X = ";&X10name
    WRITE #1;"&CLIP10_Y = ";&Y10name
    WRITE #1;"&CLIP10_Z = ";&Z10name
  if &Number_Clips = 10 then GoTo WRAPUP
    WRITE #1;"&CLIP11_X = ";&X11name
    WRITE #1;"&CLIP11_Y = ";&Y11name
    WRITE #1;"&CLIP11_Z = ";&Z11name
  if &Number_Clips = 11 then GoTo WRAPUP
    WRITE #1;"&CLIP12_X = ";&X12name
    WRITE #1;"&CLIP12_Y = ";&Y12name
    WRITE #1;"&CLIP12_Z = ";&Z12name

WRAPUP:
    CLOSE #1

 end


'---------------------------------------SUB ROUTINES--------------------------------------------
SET_STYLUS_OFFSET:
    &stylus_offset = &Z_clipbase - &Z_top_surface
	&first = 0
  return

SET_CLIPHEIGHT:    'Gets called once in the first touch
    &Z_clipbase = %(3)-&ATC_holderHeight
	JZ, %(3) + &clipHeightClearance
  return

FIND_HOLDER:
    
    &start_X = %(1)
    &start_Y = %(2)
    &start_Z = %(3)

    ON INPUT(1,1) GoTo FOUND_EDGE
	MY, &nextHolderY

    'Error finding tool holder in clip. Check all connections and try again!
	PAUSE
	GoTo FAILED_EXIT
	end

  FOUND_EDGE:
    &yEdge = %(2)

    JY, &yEdge - &safeEdge
    JZ, %(3) + &safe_lift
	MY, &yEdge + &stylus_dia
	'Then find the Holder height   
    if %(51) = 1 then GoTo CLOSED_EXIT
      ON INPUT(1,1) GoTo TOPZ
      MZ, %(3) - &safe_lift
      GoTo FAILED_EXIT
  TOPZ:
    ON INPUT(1,1)
      &Z_top_surface = %(3)
    MZ, %(3) + &safe_lift
	
    JY, &yEdge + &holder_radius + (&stylus_dia/2)
	&start_Y = %(2)
	MZ, %(3) - &safe_lift - &safeEdge
   
	GoSub CENTER_IN_XY

    ' lift to clear lip and then move to position for next holder
    MZ, %(3) + &safe_lift
    MY, %(2) + (&holder_radius * 2)
	MZ, &Z_top_surface - &safeEdge
	&nextHolderY = %(2) + &nextY

  return

CENTER_IN_XY:
    'Set up Speeds, etc
    
    'Move left and right to get end points of a chord; X center-point is at 1/2 this distance
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_LEFT
    MX, &start_X - &max_center_move
    GoTo FAILED_EXIT
    HIT_LEFT:
      ON INPUT(1,1)
      &leftstart = %(1)
      MX, &start_X
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_RIGHT
    MX, &start_X + &max_center_move
    GoTo FAILED_EXIT
    HIT_RIGHT:
      ON INPUT(1,1)
      &rightstart = %(1)
      &center_X = &leftstart + ((&rightstart - &leftstart) / 2)
      MX, &center_X

    'Move up and down to get end points of another chord; Y center-point is at 1/2 this distance
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_BOTTOM
    MY, &start_Y - &max_center_move
    GoTo FAILED_EXIT
    HIT_BOTTOM:
      ON INPUT(1,1)
      &bottomstart = %(2)
      MY, &start_Y
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_TOP
    MY, &start_Y + &max_center_move
    GoTo FAILED_EXIT
    HIT_TOP:
      ON INPUT(1,1)
      &topstart = %(2)
      &center_Y = &bottomstart + ((&topstart - &bottomstart) / 2)
      MY, &center_Y

    'Move left and right to get end points of a chord; X center-point is at 1/2 this distance
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_LEFT2
    MX, &start_X - &max_center_move
    GoTo FAILED_EXIT
    HIT_LEFT2:
      ON INPUT(1,1)
      &leftstart = %(1)
      MX, &start_X
    if %(51) = 1 then GoTo CLOSED_EXIT
    ON INPUT(1,1) GoTo HIT_RIGHT2
    MX, &start_X + &max_center_move
    GoTo FAILED_EXIT
    HIT_RIGHT2:
      ON INPUT(1,1)
      &rightstart = %(1)
      &center_X = &leftstart + ((&rightstart - &leftstart) / 2)
      MX, &center_X
	&start_X = &center_X

	'VS, &startspeed_XY, &startspeed_Z     REM BY GC 
  return

CLOSED_EXIT:
    'Input Already Triggered -- Exit
    PAUSE
    GoTo CLEANUP

FAILED_EXIT:
    'Didn't seem to Make Contact in Expected Distance -- Exit
    PAUSE
    GoTo CLEANUP

ZEROZ1:
  ON INP(&Z_Prox,&Z_State)
  VA,,,0,,,,,0
  PAUSE .1
  MZ,-&zProxClearance
return	



	
