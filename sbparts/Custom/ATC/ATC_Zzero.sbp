'ATCZZ.sbp --ATC Homing program for calculating the Z height of each tool
' 5/5/2017 DJP - Commented out dust skirt actuation, added &SafeX
' 10/18/17 GAB - Added test for ATC type 1 for dust skirt open
'                Combined to work with both PRS and PRS4 ATC
' 6/20/17 GAB - Removed SC command on line 8, command not supported
' 3/6/18 GAB - Add test for PRS4 before first plunge and if not, open dust skirt and
'               prompt to add clip to bit
'            - Add test for PRS4 before END and if not, close dust skirt and prompt
'               to remove clip from bit

  SA,           'Set to Absolute Mode
  SW, 0         'Deactivate Warning duration
 
  SO,1,0        'Make sure spindle is not running

If %(25) = 1 Then GoTo METRICUNITS
    &bigZclear = 24
		&bigZplunge = -18
		If &ATC = 1 Then &ZUP = 3    'Distance off table to pull up
    If &ATC = 2 Then &ZUP = 2
		&zZeroMoveSpeed = .75
		&ZforVA = -0.5
		&SafeX = 12
    GoTo ZZERO_INITVARS
METRICUNITS:
    &bigZclear = 610
		&bigZplunge = -460
		If &ATC = 1 Then &ZUP = 76    'Distance off table to pull up
    If &ATC = 2 Then &ZUP = 104
		&zZeroMoveSpeed = 19
		&ZforVA = -12.7
		&SafeX = 300
'Initialize Variables
ZZERO_INITVARS:
    'Initialize
  &Ztop = 0
  &TotalZ = 0
TOP:
  INPUT "Bit number to Zero:" &tool

  FP, C:\SbParts\Custom\ATC\ATC.SBP

  MS,,&zZeroMoveSpeed	    'Change move speed to .75

  SF, 0         'Deactivate software limits
  VN, 0        'Turn limit switch OFF

  'Calibrate using Prox switch on Z
  ON INP(5,0) GoSub CONTACTZ1
    MZ, &bigZclear

  ON INP(5,0) GoSub CONTACTZ2
    MZ, &bigZclear

  VN, 1        'Turn limit switch ON

  VA,,,&ZforVA,,,,,0
  If &ATC = 1 Then JX,&sx
  JY,&ATC_zZeroY-%(7)         'Y Location for Z Zeroing using Fixed Zero plate
  JX,&ATC_zZeroX-%(6)         'X Location for Z Zeroing using Fixed Zero plate
  If &ATC = 2 Then GOTO START_PLUNGE 
    SO,3,1     'Open Dust Skirt on older than PRS4 ATC
    'Press ENTER when clip is on bit shank and ready to zero
    PAUSE

START_PLUNGE:
'Set the contact switch action ... Plate NOW SET FOR Input Switch #1
  ON INP(1,1) GoSub CONTACT1
    MZ, &bigZplunge 

  PRINT, "Tool #" & &Tool & " height = " & &zHeight

  If &Tool = 1 Then &Tool1_Z = &zHeight
  If &Tool = 2 Then &Tool2_Z = &zHeight
  If &Tool = 3 Then &Tool3_Z = &zHeight
  If &Tool = 4 Then &Tool4_Z = &zHeight
  If &Tool = 5 Then &Tool5_Z = &zHeight
  If &Tool = 6 Then &Tool6_Z = &zHeight
  If &Tool = 7 Then &Tool7_Z = &zHeight
  If &Tool = 8 Then &Tool8_Z = &zHeight
  If &Tool = 9 Then &Tool9_Z = &zHeight
  If &Tool = 10 Then &Tool10_Z = &zHeight
  If &Tool = 11 Then &Tool11_Z = &zHeight
  If &Tool = 12 Then &Tool12_Z = &zHeight
  
  FP, &ATC_Writer
	
  
  &newZheight = &zHeight + &ZOffset
  VA,,,%(3)-&newZheight,,,,,&newZheight  'Set Z height of bit relative to table surface from Table Base Coordinates

  If &ATC = 1 Then MX,&sx
  If &ATC = 2 Then MX,%(1)-&SafeX

  INPUT "Do you wish to zero another cutter <Y or N>: " &gototop
  
  If &gototop = "Y" Then GoTo TOP 
  If &ATC = 2 Then END
    '***ZEROED! REMEMBER TO REMOVE ALLIGATOR CLIP!***
    PAUSE
  If &ATC = 1 Then SO,3,0

 End            'This END statement causes the program to end here without
                '  dropping through to subroutine

'================================================================Subroutines
CONTACT1:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,1)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
  
  &zHeight = %(3)
  
  If &ATC = 1 Then JZ,%(3)+&ZUP
  If &ATC = 2 Then JZ, &ZforVA      'Pull-up to 0.5" under prox switch
  'Now We're Ready to Make Slow Move for Accurate Reading
  PAUSE .2
 Return

CONTACT2:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,1)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
 Return         'Technically this RETURNs us to the next file line after the
                '   move that was interrupted by the switch ... eg. the MZ
                '   and we will then encounter the END and leave the file.

CONTACTZ1:
  ON INP(5,0)
  JZ,%(3)+&ZforVA
 Return

CONTACTZ2:
  ON INP(5,0)
  MZ,%(3)+&ZforVA
  &Ztop = %(3) 
 Return
