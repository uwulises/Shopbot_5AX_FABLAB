'Torch ON Routine for PRTalpha Plasma Cutter 9/04

'--------------------------------------------------------------------------
If %(22) = 1 THEN END     'Don't use if in preview

  SA,          'Set to Absolute Mode
  SF,0         'Turn off limit checking temporarily here
  VN,0,,1      'Disable input #3 Limit switch function and enable Torch height control
  SO,1,0       'Turn torch OFF
  SO,2,0       'Turn touch-off OFF
  MS,,0.5      'Set plunge rate    

'  &ZBOT=0.2    'Set ideal start height here (remove apostrophe) or in your file
'  &Pdelay=0.2  'Set pierce delay here (remove apostrophe) or in your file - must be at least .1 seconds

  ON INP(3,1) GOTO CONTACT1  'looks for touch off
  
  SO,2,1                  'Close relay and connect torch to input
  PAUSE .2
  JZ, -4                  'Plunge down to touch material

CONTACT1: 
  VA,,,0                  'Set Zero
     PAUSE .1  
  SO,2,0                  'Open relay and disconnect input
     PAUSE .1
  JZ, &ZBOT               'Pull-up to ideal start height
     PAUSE .1

'TORCHON
  SO,1,1                  'Close torch on relay
     &count = 0
  HOLD:
     &IN = %(52)          'status of input 2 - Torch ready signal
     &count = &count +1
     IF &count > 500 THEN GOTO ERROR
'     IF &IN = 0 THEN GOTO HOLD
     PAUSE &Pdelay        'sets pierce delay from variable in part file (or above)
     MZ,.125              'lowers starting cut height before torch height takes over
     VH,1                 'turns automatic torch height on
     VN,1                 'Enable #3 input limit switch function ON 
  
  END

ERROR:

  '       ***ERROR GETTING FEEDBACK FROM TORCH***
  PAUSE

 