'5AX_GET_GAUGE_FACTOR.SBP 
'Used to set the gauge factor of the 5 axis tool. The gauge factor is the distance between
'when the y axis hits the prox switch and when the guage hits the side plate. It is used
'to calculate the tool length routine.

'Author: Matt Schmitz
'Date Created: 9/11/2015
'Edits: 

'SET UP ---------------------------------------------------------------------------------

'Basic Checks and Set-Up
	SL 'clear user variables
	SA, 'Absolute Mode
	SF,0 'Temporarily Turn off limit checking (will return to whatever it's previous state was after file closes)
	VN,0 'Temporarily deactivate limit switch functionality (")
	SO,1,0 'spindle 1 off
	SO,2,0 'Spindle 2 off
	&modenow = %(22) 'check if we're in Preview or Move/Cut mode
	IF &modenow = 1 then GoTo CHANGE_MODE_EXIT 'subroutine asks user to change mode and restart
	CN,90 'Load the my_variables file via Custom Cut 90

'Set Units: 
	&units = 1 ' defaults to inches
	IF %(25) = 1 THEN &units = 25.4 'set to mm

	'check to make sure your still using the same units as when you started calibration:
	IF %(25) <> &5AX_calibration_units THEN GOTO Change_Units 

'Variable Initializations
	'Move and jog speeds for this routine
	&move_speed_XYZ = .5 * &units
	&move_speed_AB = 10 'degrees		
	&jog_speed_XYZ = 3 * &units
	&jog_speed_AB = 40 'degrees

	&back_off_XYZ = 1 * &units
	&big_move_XYZ = 100 * &units
	&small_move_XYZ = &back_off_XYZ + .25 * &units 'go just past the back-off distance
	&y_offset = 4 * &units
	&TrgtHIT = 0 'normally closed switch
    &Ytrgt = 3 'input for y prox switch
    &Ztrgt = 5 'input for z prox switch
	&plate_input = 1 'input for side plate

'Set Move and Jog Speeds
	VS,&move_speed_XYZ,&move_speed_XYZ,&move_speed_AB,&move_speed_AB,&jog_speed_XYZ,&jog_speed_XYZ,&jog_speed_AB,&jog_speed_AB 


'MOVEMENT ----------------------------------------------------------------------
GOSUB Prompt_Start 'go through checks to make sure the user is ready to run routine
GOSUB Find_Height 'moves tool up to the correct height
J5,,,,0,0 'make sure spindle is pointing down
GOSUB Find_Y 'locates y prox and backs off a safe distance
J5,,,,0,-90 'point the spindle in negative y direction
GOSUB Get_Gauge_Factor 'ask user to locate the side plate
GOSUB Write_Data 'write plate location to my_variables
JY, %(2) + &back_off_XYZ 'back off side plate
J5,,,,0,0 'jog to spindle pointing down
GOSUB Completion_Message 
END	'End program (prevents it from dropping through subroutines)	

'SUBROUTINES ---------------------------------------------------------------------------
Change_Units:
	MSGBOX (You are using different units (inches/mm) then when you ran the 5-Axis AB Calibration Routine. The calibration routines must be run sequentially and using the same units. Please exit and change units - or re-start the calibration sequence., Exclamation, Incorrect Units)
	END

Prompt_Start:
	MSGBOX (Is the aligator clip on the spindle?, OKCancel, Check #1)
	IF &msganswer = Cancel THEN END
	MSGBOX (Has the collet been removed from the spindle?, OKCancel, Check #2)
	IF &msganswer = Cancel THEN END
	RETURN

Find_Height: 
	ON INP(&Ztrgt,&TrgtHIT) GOTO Zcontact 'z prox  
    JZ, %(3) + &big_move_XYZ
	&current_axis = Z
	GOTO Failed_Contact_Exit 'if you make it here, you didn't contact anything
	Zcontact: 'This is our subroutine for action after hitting the prox switch
	    JZ, %(3) - &back_off_XYZ 'Move back out for another pass
	    ON INP(&Ztrgt,&TrgtHIT) GOTO Zagain
	    MZ, %(3) + &small_move_XYZ 'move back toward switch
		&current_axis = Z
		GOTO Failed_Contact_Exit 'if you make it here, you didn't contact anything
		Zagain:
			JZ, %(3) - &5AX_side_plate_height 'Jog to correct height 
	RETURN       

Find_Y: 'locates y prox and backs off a safe distance
	ON INP(&Ytrgt,&TrgtHIT) GOTO Ycontact1 'y prox
    JY, %(2) - &big_move_XYZ
	&current_axis = Y
	GOTO Failed_Contact_Exit 'if you make it here, you didn't contact anything
	Ycontact1:'This is our subroutine for action after hitting the prox switch
		    JY, %(2) + &back_off_XYZ 'Move back out for another pass
		    ON INP(&Ytrgt,&TrgtHIT) GOTO Yagain
		    MY, %(2) - &small_move_XYZ 'move back toward switch
			&current_axis = Y
			GOTO Failed_Contact_Exit 'if you make it here, you didn't contact anything
			Yagain: 
				&y_prox_contact = %(2)
				JY, %(2) + &y_offset
	RETURN  

Get_Gauge_Factor:
	ON INP(&plate_input,1) GOTO Ycontact2 'y prox
    MY, %(2) - &big_move_XYZ
	&current_axis = Y
	GOTO Failed_Contact_Exit 'if you make it here, you didn't contact anything
	Ycontact2: 'This is our subroutine for action after hitting the prox switch
		&plate_contact = %(2)
		&gauge_factor =  &plate_contact - &y_prox_contact
		RETURN

Write_Data:
	&tempfile = "c:\SbParts\TempVar.txt"
	OPEN &tempfile for OUTPUT AS #1
	WRITE #1;"&5AX_gauge_factor = "; &Gauge_Factor
	CLOSE #1
	PAUSE .5
	SHELL "C:\SbParts\Custom\MyVars.exe c:\SbParts\TempVar.txt"
	RETURN

Completion_Message:
	MSGBOX (The gauge factor has been stored in C:\SbParts\Custom\My_Variables.sbc, OKOnly, Routine Complete)
	RETURN

Failed_Contact_Exit:
	MSGBOX(The &current_axis axis failed to make contact with a proximity switch in the expected distance.,exclamation, Aborting)
	END

Change_Mode_EXIT:
	'You are in Preview mode - please switch to Move/Cut mode and retry.
    PAUSE 
	END    