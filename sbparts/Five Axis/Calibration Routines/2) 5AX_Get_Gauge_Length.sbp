'5AX_Get_Gauge_length.SBP 
'Uses the Calibration block calculate the gauge length of the tool

'Author: Matt Schmitz
'Date Created: 9/15/2015
'Edits: 
	'9/15/15 - Matt Schmitz

'SET UP ---------------------------------------------------------------------------------

'Basic Checks and Set-Up
	SL 'clears user variables
	SA, 'Absolute Mode
	SF,0 'Temporarily Turn off limit checking (will return to whatever it's previous state was after file closes)
	VN,0 'Temporarily deactivate limit switch functionality (")
	SO,1,0 'spindle 1 off
	SO,2,0 'Spindle 2 off
	&modenow = %(22) 'check if we're in Preview or Move/Cut mode
	IF &modenow = 1 then GoTo CHANGE_MODE_EXIT 'subroutine asks user to change mode and restart
	CN,90 'read from my variables to get &5AX_calibration_plate_width

'Set Units
	&units = 1 'inches
	IF %(25) = 1 THEN &units = 25.4 'change units to MM

	'check to make sure your still using the same units as when you started calibration:
	IF %(25) <> &5AX_calibration_units THEN GOTO Change_Units 
	
'Variable Initializations
	'Move and jog speeds for this routine
	&move_speed_XYZ = .5 * &units
	&move_speed_AB = 10 'degrees		
	&jog_speed_XYZ = 2 * &units
	&jog_speed_AB = 40 'degrees

	&z_plunge = 10 * &units
	&back_off = .8 * &units 
	&move_past_dist = 7.5 * &units
	&move_down_dist = 7.5 * &units
	&TrgtHIT = 0 'normally closed switches

	&plate_input = 1 'input that the plate is hooked up to
	&plate_sysvar = 50 + &plate_input 'system variable for input 

'ROUTINE --------------------------------------------------------------------------------
'Go through checks to make sure user is ready to run routine: 
	GOSUB PROMPT_START
	
'set speeds and position axes
	VS, &move_speed_XYZ, &move_speed_XYZ, &move_speed_AB, &move_speed_AB, &jog_speed_XYZ, &jog_speed_XYZ, &jog_speed_AB, &jog_speed_AB
	J5,,,,-90,0 'jog b axis to zero, a to -90
	Z3 'zero x y and z

'Zero Z - ends just above plate
	GOSUB Z_Zero
'calculate gauge length 
	GOSUB Get_Gauge_Length
'write it to my_variables
	GOSUB Write_Gauge_Length
'print completion message
	GOSUB Completion_Message
	END

'SUBROUTINES ----------------------------------------------------------------------------
Change_Units:
	MSGBOX (You are using different units (inches/mm) then when you ran the 5-Axis AB Calibration Routine. The calibration routines must be run sequentially and using the same units. Please exit and change units - or re-run the AB Calibration Routine., Exclamation, Incorrect Units)
	END

Prompt_Start:
	MSGBOX (Are you above the calibration block with the spindle pointing straight down?, OKCancel, Check #1)
	IF &msganswer = Cancel THEN GOSUB TIDY_UP
	IF &msganswer = Cancel THEN END
	MSGBOX (Have the drill rod and collet nut been removed from the spindle?, OKCancel, Check #2)
	IF &msganswer = Cancel THEN GOSUB TIDY_UP
	IF &msganswer = Cancel THEN END
	MSGBOX (Is the alligator clip on the spindle? Is the connector plugged in?, OKCancel, Check #3)
	IF &msganswer = Cancel THEN GOSUB TIDY_UP
	IF &msganswer = Cancel THEN END
	RETURN

Z_Zero:
	IF %(&plate_sysvar) = 1 THEN GOTO CLOSED_EXIT 'check if input is already closed
	ON INPUT(&plate_input, 1) GOTO ZCONTACT 'start checking for input
		MZ, - &Z_plunge 'make a big plunge
		GOTO FAILED_CONTACT_EXIT 'if you make it here, you didn't contact anything
		ZCONTACT:
			ZZ 'zero the z axis
	JZ, &back_off 'lift up a bit to be ready for next move
	RETURN

Get_Gauge_Length:
	'right side:
	J5,,-&move_past_dist,,0,90
	JZ, -&move_down_dist
	IF %(&plate_sysvar) = 1 THEN GOTO CLOSED_EXIT 'check if input is already closed
	ON INPUT(&plate_input, 1) GOTO RIGHT_SIDE_CONTACT 'start checking for input
		MY, 0 'move in to touch
		GOTO FAILED_CONTACT_EXIT 'if you make it here, you didn't contact anything
		RIGHT_SIDE_CONTACT:
			&contactR = %(2)
	'left side
	JY, &contactR - &back_off
	JZ, &back_off
	J5,,&move_past_dist,,,-90
	JZ, -&move_down_dist
	IF %(&plate_sysvar) = 1 THEN GOTO CLOSED_EXIT 'check if input is already closed
	ON INPUT(&plate_input, 1) GOTO LEFT_SIDE_CONTACT 'start checking for input
		MY, 0 'move in to touch
		GOTO FAILED_CONTACT_EXIT 'if you make it here, you didn't contact anything
		LEFT_SIDE_CONTACT:
			&contactL = %(2)

	&gauge_length = (&contactL - &contactR - &5AX_calibration_plate_width) /2

	JY, &contactL + &back_off
	JZ,0
	J5,0,0,&back_off,0,0
	RETURN

Write_Gauge_Length:
	'write to my_variables  
    &tempfile = "C:\SbParts\TempVar.txt"
	OPEN &tempfile FOR OUTPUT AS #1
    WRITE #1; "&5AX_Gauge_Length = "; &Gauge_Length
    CLOSE #1
    PAUSE .5 
	Shell "C:\SbParts\Custom\MyVars.exe c:\SbParts\TempVar.txt"
	RETURN

COMPLETION_MESSAGE:	
	'print values
	PRINT "Gauge Length = ",&Gauge_Length 
	'PRINT " "
	PRINT "Value saved as &5AX_Gauge_Length to C:\SBPARTS\CUSTOM\my_variables.sbc"
	'print message
	'MSGBOX (The Gauge Length has been calculated. Its value has been saved, OKOnly, Finished)
	RETURN

TIDY_UP:
	'wraps up any loose ends before exiting/completion of program
	ON INPUT(&3zero_input,1) 'clear input switch
	RETURN

CHANGE_MODE_EXIT:
	'exit for if user is in preview mode
	GOSUB TIDY_UP 
    'You are in Preview mode - please switch to Move/Cut mode and retry.
	PAUSE 
	END	

CLOSED_EXIT:
	ON INPUT(&3zero_input,1) 'turn off switch check
	GOSUB TIDY_UP
	'Input is already trigered - Exiting
	PAUSE
	END

FAILED_CONTACT_EXIT:
	GOSUB TIDY_UP
	'Didn't make contact in expected distance - Exiting
	PAUSE
	END


