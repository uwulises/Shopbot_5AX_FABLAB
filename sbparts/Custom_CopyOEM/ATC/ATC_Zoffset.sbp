'ATC Offset calculator v1.080405
'    Gordon Bergfors, ShopBot Tools, Inc.
'     All Rights Reserved, Copyright 2002-2017
'	Speed changes on touch off lines 50 and 54 BHM 20Oct06  
'   10/18/17 GAB Combine for PRS and PRS4 ATCs
'Determine whether we are in Preview mode or Move/Cut mode
   &modenow = %(22)
   IF &modenow = 1 THEN GOSUB Changemode

    SA,     
    SW, 0         'Deactivate Warning duration
    SF, 0         'Deactivate software limits

    SO,1,0        'Make sure spindle is not running

'Initialize
if %(25) = 1 Then Goto METRICUNITS
    &ZUP = 0.5    'Distance off table to pull up
	  &bigZ = 16
	  &jogZset = 1
	  &moveZset = .75
	  &zClearMove = .50
	Goto FINISHINIT
METRICUNITS:
    &ZUP = 12    'Distance off table to pull up
	  &bigZ = 400
	  &jogZset = 25
	  &moveZset = 19
	  &zClearMove = 12

FINISHINIT:
    &th_z = 0     'Initialize to zero

    C#,90         'Load the custom variables file from Custom Cut 90
    C#,89         'Read variables from ATC_Variables and ATC_Tool_Variables
    C#,88         'Set speeds and scale depending on unit type

'Check status of input #3
    &prox_status = %(53)
    IF &prox_status = 0 THEN GOSUB Getoffprox

' set the location to move to before second contact
   
    VA,,,%(3)+%(8),,,,,0             'Return to Table Base Coordinate system

    IF &ToolIN = 1 THEN &th_z = &Tool1_Z
    IF &ToolIN = 2 THEN &th_z = &Tool2_Z
    IF &ToolIN = 3 THEN &th_z = &Tool3_Z
    IF &ToolIN = 4 THEN &th_z = &Tool4_Z
    IF &ToolIN = 5 THEN &th_z = &Tool5_Z
    IF &ToolIN = 6 THEN &th_z = &Tool6_Z
    IF &ToolIN = 7 THEN &th_z = &Tool7_Z
    IF &ToolIN = 8 THEN &th_z = &Tool8_Z

    VA,,,%(3)-&th_z,,,,,&th_z

'Place ground clip on bit and zero plate under - press ENTER to continue
    PAUSE
'Set the contact switch action ... Plate NOW SET FOR Input Switch #1
    ON INP(1,1) GOSUB CONTACT1
      JS,,&jogZset	   		 'Change jog speed to .75

	  JZ, -&bigZ

    ON INP(1,1) GOSUB CONTACT2 'this is where we'll go on contact with plate
      MS,,&moveZset	    	 'Change move speed to .75

	  MZ, -&bigZ

'We're done with all moves so reset speeds
    VS,&start_XYmove_Speed,&start_Zmove_speed,,,&start_XYjog_Speed,&start_Zjog_speed         '... and assign slower speeds

    &zOffset = %(3) - &my_ZzeroThickness

    VA,,,,,,,,0             'Return to Table Base Coordinate system

    JZ, %(3)+ &ZUP 'Jog to &ZUP to clear plate
  
    FP, &ATC_Writer

    VA,,,%(3)-&zOffset,,,,,&th_z+&zOffset  'Set Z height of bit relative to table surface from Table Base Coordinates

END            'This END statement causes the program to end here without
                '  dropping through to subroutine

'================================================================Subroutines
CONTACT1:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,0)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
  JZ, %(3)+&zClearMove  'Pull-down SLIGHTLY ... 
  'Now We're Ready to Make Slow Move for Accurate Reading
  PAUSE .2
 RETURN

CONTACT2:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,0)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
 RETURN         'Technically this RETURNs us to the next file line after the
                '   move that was interrupted by the switch ... eg. the MZ
                '   and we will then encounter the END and leave the file.

Getoffprox:
   'Use Arrow Keys to move Off of Prox Switch (ESC when Clear)
    PAUSE
    SK    
 RETURN
