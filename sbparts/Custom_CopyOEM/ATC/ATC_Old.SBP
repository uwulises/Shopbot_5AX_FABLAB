'Primary Tool Changer Module v2.032305
'-----------------------------------------------------------------------------------------------------------------
'     Gordon Bergfors, ShopBot Tools, Inc.
'     All Rights Reserved, Copyright 2002-2005
'8/2/05 GAB - Add tool height adjustment for using zero off top or table.
'   &zOffset is subtracted from &th when tool height is set off prox switch
'2/1/06 GAB - Add offset compensation for an auxillary home position to make
'   tool changes occur properly per Brian for Newman Whitmire.
'3/29/06 GAB - Add additional variables for locations, heights and table base
'   offsets to implement a new atc_variables file that is created by Bill's 
'   setup routine and changed by a new Z zeroing file.
'6/28/06 TH - Simplify TBC usage
'7/10/06 GAB - Add zero tool routines
'            - Handle no tool in spindle by verifying settings and picking up new tool
'4/24/07 GAB - 
'5/10/07 BHM - Added dust skirt functions
'-----------------------------------------------------------------------------------------------------------------
'                                         for ATC inp 6 = Status of spindle turning [0=TURNING; 1=OFF]
'                                         for ATC inp 7 = Status of ToolHolder in spindle [0=ABSENT; 1=PRESENT]
'                                         for ATC inp 8 = Status of drawbar [0=OPEN; 1=CLOSED]
'                                         for ATC OUT 1 = Run Spindle 1
'                                         for ATC OUT 2 = Run Spindle 2
'                                         for ATC OUT 3 = Dust Skirt Activate
'                                         for ATC OUT 5 = [speed]
'                                         for ATC OUT 6 = [speed]
'                                         for ATC OUT 7 = [speed]
'                                         for ATC OUT 8 = Tool Release
'-----------------------------------------------------------------------------------------------------------------
  'Initialize.....................................................................................................

    SA,     
    &oldtool=0
    &newtool=-1
    FP, c:\SbParts\Custom\ATC\ATC_Load_All_Values.sbp
    SF,0   ''###?? How to handle limit checking if already off by user choice??
    
    &oldtool = &ToolIN
    &prox_status = %(53)
    IF &prox_status = 0 THEN GOSUB Getoffprox
     
VN,0
     ON INP(5,0) GOTO ZisHigh
	JZ,20
ZisHigh:
JZ,%(3)-.5
&ATC_safeclear = %(3)

'VN,1

  'Preliminary Checks..............................................................................................
      
    IF %(101) = 0 THEN GOTO Next_Check01    'SPINDLE ON?
        SO,1,0
        PAUSE 1
     Next_Check01:  
      
    IF %(56) = 1 THEN GOTO Next_Check02     'SPINDLE Turning?
        SO,1,0
        PAUSE 1
     Next_Check02:
      
    IF %(101) = 1 THEN GOTO spindlestoperr  'EXIT IF WE COULDN'T STOP!
    IF %(56) = 0 THEN GOTO spindlestoperr
    
    FP, c:\SbParts\Custom\ATC\ATC_check_status.sbp
    PAUSE .5                                'Allow reading status check
    VD,                                     'Remove status check because it won't stay current (could display at end)
    IF &ATC_status = OK THEN GOTO OK_SWAP_TOOLS
    IF &ATC_status = EMPTY THEN GOTO settool0 
PRINT, "ATC_Status is " & &ATC_status

    ''### may need a better error handler here if we drop through
    'Confused ATC Situation
    'Error setting up for tool change. ATC(58)
    PAUSE 
 END

OK_SWAP_TOOLS:  'NOW SWAP TOOLS ////////////////////////////////////////////////////////////////////////////////////////
   '   Routine presumes that there is a known tool number present in tool holder, and that a request has been
   '     received to swap to another (legal) tool number. If same number, then procedure is just passed through. 

Position_Z:
	
    'JZ, &ATC_SAFEY   

   'Update Offsets
    IF &ToolIN=0 THEN GOTO settool0
    IF &ToolIN=1 THEN GOTO settool1
    IF &ToolIN=2 THEN GOTO settool2
    IF &ToolIN=3 THEN GOTO settool3
    IF &ToolIN=4 THEN GOTO settool4
    IF &ToolIN=5 THEN GOTO settool5
    IF &ToolIN=6 THEN GOTO settool6
    IF &ToolIN=7 THEN GOTO settool7
    IF &ToolIN=8 THEN GOTO settool8

Droptool:
    
    IF &Tool = &ToolIN THEN GOTO Finish_ToolChange 'NO NEED TO DO TOOL CHANGE
    J2, &tx, &sy          'Jog to X zero and Y midpoint between tool holders
    SO,3,1			  'Open Dust skirt 
    &clipHeight = &tz - %(8)
    JZ, &clipHeight       'Jog to Z height of toolholder clip
    C#,88                 'Save current speeds and set to ATC speeds
    MY, &ty               'Move into clip

    SO, 8,1            'Open toolholder chuck
    PAUSE .5

    MZ, %(3)+ 0       'Move up to clear toolholder
    
    IF %(58) = 0 THEN GOTO bartoperror

    MZ, %(3)+ 0.5       'Move up to clear toolholder
    
    SO, 8,0            'Close toolholder chuck
    PAUSE 1

    MZ, %(3)+ 0       'Move up to clear toolholder

    IF %(58) = 1 THEN GOTO barboterror
      
ZeroToolDrop:    
    &newtool = &tool 'Set new tool number
    &Dropped=1
    &ToolIN = &newTool '###?? Continue to replace var in following table and make sub 
                       '      --  Ted changed location of this update so TOOlIN stays consistent with offsets    
    IF &newtool=0 THEN GOTO settool0
    IF &newtool=1 THEN GOTO settool1
    IF &newtool=2 THEN GOTO settool2
    IF &newtool=3 THEN GOTO settool3
    IF &newtool=4 THEN GOTO settool4
    IF &newtool=5 THEN GOTO settool5
    IF &newtool=6 THEN GOTO settool6
    IF &newtool=7 THEN GOTO settool7
    IF &newtool=8 THEN GOTO settool8

    GOSUB Exit_Resets ''##?? Error Handler needed here??
END    'Exit if invalid tool number

Changetool:

  'Setup new Z height of bit relative to table surface from Table Base Coordinates
    &new_curZ = %(3) - ((&th + &zOffset) - %(8))  'update to current Z
    VA,,,&new_curZ,,,,,&th + &zOffset
    
  'Skip pick up if zero tool number and leave spindle empty
    IF &newtool = 0 THEN GOTO Finish_ToolChange
     
  'Write all updated variables before starting next move
    FP, &ATC_Writer
    &clipHeight = &tz - %(8)    
    JZ, &ATC_safeclear '&clipHeight + 6.5    'Jog to clear toolholder if not zero tool
    J2, &tx, &ty             'Jog X to toolholder X
    JZ, &clipHeight + 1      'Jog Z to 1 inch over toolholder height
    SO, 8,1                  'Open toolholder chuck
    PAUSE 0.2
    IF %(58) = 0 THEN GOTO bartoperror
    MZ, &clipHeight          'Move to toolholder height  
    SO, 8,0                  'Close toolholder chuck 
    PAUSE 0.2
    
        &count=0
    Sol2:
        &count=&count+1      'Allow a little time to detect tool holder 
        PAUSE 1
        IF &count=10 THEN GOTO Barboterror
      ''###IF %(57)=0 THEN GOTO Sol2

    MY, &sy                  'Move tool out of clip
    'JZ, &ATC_safeclear

     ON INP(5,0) GOTO ZisHigh2
	JZ,20
ZisHigh2:
JZ,%(3)-.5



    SO,3,0			     'Close Dust skirt    
    &Dropped = 0  

Finish_ToolChange:
      IF &ToolIN = 0 THEN JZ,&ATC_safeclear
    'Write all updated variables
      FP, &ATC_Writer
      FP, c:\SbParts\Custom\ATC\ATC_check_status.sbp
      VD,
     GOSUB Exit_Resets

END
'END of ATC Main Routine =================================================================================================

settool0:
  &tx= 0                           'Tool clip X location  
  &ty= 0                           'Tool clip Y location   
  &tz= -11                         'Tool clip Z location
  &th= &Tool0_Z                    'Height of bit off table relative to top Prox
  &sy= 0                           'Safe Y position for tool change
  IF &newtool = 0 THEN GOTO Changetool 
GOTO ZeroToolDrop
  
settool1:
  &tx= &Clip1_X - &xOffset - %(6)  'Tool clip X location  
  &ty= &Clip1_Y - &yOffset - %(7)  'Tool clip Y location   
  &tz= &Clip1_Z                     'Tool clip Z location
  &th= &Tool1_Z                    'Height of bit off table relative to top Prox
  &sy= &ty + &ATC_safeY                'Safe Y position for tool change
  SO,5,1                           'Set OUTPUTS for speed setting
  SO,6,0                           '
  SO,7,0                           '
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool2:
  &tx= &Clip2_X - &xOffset - %(6)
  &ty= &Clip2_Y - &yOffset - %(7)
  &tz= &Clip2_Z 
  &th= &Tool2_Z 
  &sy= &ty - &ATC_safeY
  SO,5,0
  SO,6,1
  SO,7,0
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool3:
  &tx= &Clip3_X - &xOffset - %(6)
  &ty= &Clip3_Y - &yOffset - %(7)
  &tz= &Clip3_Z
  &th= &Tool3_Z 
  &sy= &ty + &ATC_safeY
  SO,5,1
  SO,6,1
  SO,7,0
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool4:
  &tx= &Clip4_X - &xOffset - %(6)
  &ty= &Clip4_Y - &yOffset - %(7)
  &tz= &Clip4_Z
  &th= &Tool4_Z
  &sy= &ty - &ATC_safeY
  SO,5,0
  SO,6,0
  SO,7,1
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool5:
  &tx= &Clip5_X - &xOffset - %(6)
  &ty= &Clip5_Y - &yOffset - %(7)
  &tz= &Clip5_Z 
  &th= &Tool5_Z
  &sy= &ty + &ATC_safeY
  SO,5,1
  SO,6,0
  SO,7,1
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool6:
  &tx= &Clip6_X - &xOffset - %(6)
  &ty= &Clip6_Y - &yOffset - %(7)
  &tz= &Clip6_Z 
  &th= &Tool6_Z
  &sy= &ty - &ATC_safeY
  SO,5,0
  SO,6,1
  SO,7,1
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool7:
  &tx= &Clip7_X - &xOffset - %(6)
  &ty= &Clip7_Y - &yOffset - %(7)
  &tz= &Clip7_Z
  &th= &Tool7_Z
  &sy= &ty - &ATC_safeY
  SO,5,0
  SO,6,1
  SO,7,1
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool

settool8:
  &tx= &Clip8_X - &xOffset - %(6)
  &ty= &Clip8_Y - &yOffset - %(7)
  &tz= &Clip8_Z
  &th= &Tool8_Z
  &sy= &ty - &ATC_safeY
  SO,5,0
  SO,6,1
  SO,7,1
  IF &Dropped = 0 THEN GOTO Droptool
 GOTO Changetool


toolerror:
  GOSUB Exit_Resets
  PRINT "Error grabbing tool check tool changer. Any key to exit (ATC262)"
  PAUSE
  END

bartoperror:
  SO, 8,0  'Close toolholder chuck, as may be left on/open
  GOSUB Exit_Resets
  PRINT "Drawbar didn't OPEN properly. Any key to exit (ATC269)"
  PAUSE
  END

barboterror:
  GOSUB Exit_Resets
  PRINT "Drawbar didn't CLOSE properly. Any key to exit (ATC275)"
  PAUSE
  END

spindleerror:
  GOSUB Exit_Resets
  PRINT "Check system for Spindle Error. Any key to exit (ATC281)"
  PAUSE
  END

spindlestoperr:
  GOSUB Exit_Resets
  PRINT "Error stopping spindle. Any key to exit (ATC287)"
  PAUSE
  END

seterr:
  GOSUB Exit_Resets
  PRINT "Error setting tool number. Any key to exit (ATC293)"
  PAUSE
  END
  
'SUBROUTINES ===========================================================================================================

CONTACTZ:
  ON INP(5,0)  
RETURN

Getoffprox:
   'Use Arrow Keys to move Off of Prox Switch (ESC when Clear)
    PAUSE
    SK    
 RETURN

Exit_Resets:
    'Reset speeds and other stuff
     FP, C:\SBPARTS\custom\ATC\ATC_restore_settings.sbp
RETURN
