'Use of Centering Routines to Set Spindle Offsets
'07/25/06 th

' The following licensing information must accompany this file at all times.

' ********** Licensing Information **********

' Copyright 2006 ShopBot Tools
'
' We encourage ShopBot users to modify this part file to suit their own needs and have licensed it 
' under the Creative Commons Atribution 3.0 license.  This license allows you to copy, distribute, 
' or modify the file with certain restrictions. Any other use requires permission from ShopBot Tools.
'
' A simplified explanantion of this license is available at  
' http://creativecommons.org/licenses/by/3.0/us/ 
'
' and the full legal code can be found at 
' http://creativecommons.org/licenses/by/3.0/us/legalcode

' We encourage you to read this legal code before modifying or distributing  this file, 
' ESPECIALLY sections 5 and 6 that cover warranties and liability.
'
' If you plan on distributing your new version, you must also state in the header of your file that it
' is a modification of a file supplied by Shopbot Tools, supply information on finding the original file, 
' and rename the file to make it clear to the user that it is a modified version. 

'
' ********** End Licensing Information **********

' User takes responsibility for the effects and outcomes of the use of this program, and ShopBot Tools assumes
' no liability it's use.

VD,  ,,,,,,,,,,, 0, -7500,  11355,  4575

PRINT "OPERATOR >>> Put a 1/4" drill rod in each spindle"
PRINT "OPERATOR >>> Set each Z to about .5 inches physically above the table."
PRINT "OPERATOR >>> Attach Z-Zero Plate with Hole securely to a convenvient place on table."
PRINT
PRINT "OPERATOR >>> Use the arrow keys to move Z Spindle rod over the test hole."
PRINT "OPERATOR >>> Attach the Ground connector to it. ESC when finished with Keypad."

'Initialize Variables
     &Z_test_depth = -.130 / 2  'This is test depth that will be used when we've set Z height accurately
     &A_test_depth = -.130 / 2  'This is test depth that will be used when we've set Z height accurately
     &startspeed_XY = %(71)
     &startspeed_Z = %(73)
     &startspeed_A = %(74)
     'Initialize some other variables that will be used
     &leftstart = 0
     &rightstart = 0
     &bottomstart = 0
     &topstart = 0
     &Z_top_surface = 0
     &A_top_surface = 0
 
     SK

PRINT "OPERATOR >>> Rod for first spindle should be over Z-Zero Plate hole."
'Ready to Continue with Centering?
PAUSE

'Set up Speeds, etc
     VS, .4, .4,.4
     VA,,,.5,.5
     &start_X = %(1)
     &start_Y = %(2)
     &start_Z = %(3)
     VD

'Get Z Set to Perfect Centering Height (ASSUMES WE ARE STARTING NEAR CENTER OF HOLE)
          MZ &start_Z + .25
          MX, &start_X + 1.00                  'Move over to Top of Material
     'Then TopZ for material thickness   
        If %(51) = 1 THEN GOTO ClosedExit
        On Input(1,1) GOTO TopZ
        MZ, &start_Z -2
        GOTO FailedExit
        TopZ:
          On Input(1,1)
          &Z_top_surface = %(3)
          MZ, &Z_top_surface + .25
          M2, &start_X, &start_Y
          MZ, &Z_top_surface + &Z_test_depth  'Position to continue test at

'Do Centering
     GOSUB Center_in_XY
     GOSUB Center_in_XY
     MZ, &start_Z

'Call it XY Zero
     VA, 0,0
     VS, &startspeed_XY, &startspeed_Z, &startspeed_A 

PRINT "OPERATOR >>> Use the arrow keys to move A Spindle rod over the test hole."
PRINT "OPERATOR >>> Attach the Ground connector to it. ESC when finished with Keypad."

     SK

'Set up Speeds, etc
     VS, .4, .4,.4
     VA,,,.5,.5
     &start_X = %(1)
     &start_Y = %(2)
     &start_A = %(4)
     VD

PRINT "OPERATOR >>> Rod for second spindle should be over Z-Zero Plate hole."
'Ready to Continue with Centering?
PAUSE

'Get A Set to Perfect Centering Height (ASSUMES WE ARE STARTING NEAR CENTER OF HOLE)
          MA &start_A + .25
          MX, &start_X + 1.00                  'Move over to Top of Material
     'Then TopA for material thickness   
        If %(51) = 1 THEN GOTO ClosedExit
        On Input(1,1) GOTO TopA
        MA, &start_A - 2
        GOTO FailedExit
        TopA:
          On Input(1,1)
          &A_top_surface = %(4)
          MA &A_top_surface + .25
          M2, &start_X, &start_Y
          MA, &A_top_surface + &A_test_depth  'Position to continue test at

'Do Centering
     GOSUB Center_in_XY
     GOSUB Center_in_XY
     MA, &start_A


'Clean Up
     VS, &startspeed_XY, &startspeed_Z, &startspeed_A 
     MZ &start_Z + .5
     MA &start_A + .5

VD
PRINT "Your X Offset is:  ", %(1) * -1
PRINT "Your Y Offset is:  ", %(2) * -1
PRINT
PRINT "ENTER these Values in your ShopBot Database using TOOLS > ShopBot SETUP (5th screen)."
PRINT "ENTER next and click on I'M DONE"



END


'----------------------------------------------------------------------SUBS

Center_in_XY:

   'Move left and right to get end points of a chord; X center-point is at 1/2 this distance
     If %(51) = 1 THEN GOTO ClosedExit
     On Input(1,1) GOTO HitLeft
     MX, &start_X - 2
     GOTO FailedExit
     HitLeft:
        On Input(1,1)
        &leftstart = %(1)
        MX, &start_X
     If %(51) = 1 THEN GOTO ClosedExit
     On Input(1,1) GOTO HitRight
     MX, &start_X + 2
     GOTO FailedExit
     HitRight:
        On Input(1,1)
        &rightstart = %(1)
        MX, &start_X
        &center_X = &leftstart + ((&rightstart - &leftstart) / 2)
        MX, &center_X

   'Move up and down to get end points of another chord; Y center-point is at 1/2 this distance
     If %(51) = 1 THEN GOTO ClosedExit
     On Input(1,1) GOTO HitBottom
     MY, &start_Y - 2
     GOTO FailedExit
     HitBottom:
        On Input(1,1)
        &bottomstart = %(2)
        MY, &start_Y
     If %(51) = 1 THEN GOTO ClosedExit
     On Input(1,1) GOTO HitTop
     MY, &start_Y + 2
     GOTO FailedExit
     HitTop:
        On Input(1,1)
        &topstart = %(2)
        MY, &start_Y
        &center_Y = &bottomstart + ((&topstart - &bottomstart) / 2)
        MY, &center_Y

  RETURN

ClosedExit:
      'Input Already Triggered -- Exit
      PAUSE
      END

FailedExit:
      'Didn't seem to Make Contact in Expected Distance -- Exit
      PAUSE
      END
