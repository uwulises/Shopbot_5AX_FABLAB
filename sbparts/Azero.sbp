' The following licensing information must accompany this file at all times.
' ********** Licensing Information ******************************************************
' Copyright 2001 - 2014 ShopBot Tools
' We encourage ShopBot users to modify this part file to suit their own needs and have licensed it 
' under the Creative Commons Atribution 3.0 license.  This license allows you to copy, distribute, 
' or modify the file with certain restrictions. Any other use requires permission from ShopBot Tools.
'
' A simplified explanantion of this license is available at  
' http://creativecommons.org/licenses/by/3.0/us/ 
' and the full legal code can be found at 
' http://creativecommons.org/licenses/by/3.0/us/legalcode
' We encourage you to read this legal code before modifying or distributing  this file, 
' ESPECIALLY sections 5 and 6 that cover warranties and liability.
'
' If you plan on distributing your new version, you must also state in the header of your file that it
' is a modification of a file supplied by Shopbot Tools, supply information on finding the original file, 
' and rename the file to make it clear to the user that it is a modified version. 
' ********** End Licensing Information **************************************************
'
'****************************************************************************************
'AZERO.SBP -- Zero A Axis on Contact with Zeoring Plate
'Copyright 1998-2014. ShopBot Tools, Inc.
'----------------------------------------------------------------------------------------
'*NOTE* This file will always be over-written during software update installations to insure
'        it is kept standard and current.
'*NOTE* If you want to make a custom version of this file that is called with the C2 Command
'        you should save it with a different name, say "ZZERO_Joes_8_23_08.sbp", then change the
'        C2 Command (found in C:\SbParts\Custom\Custom2.sbc to call your file rather than the
'        standard file. You may need to update this Custom2.sbc file when you update software.
'----------------------------------------------------------------------------------------
'!!Table Base Coordinates are also Zeroed During Routine (MODIFIED FROM ZZERO ROUTINE 6/10/4)
'Variables in this file are read from the my_variables.sbc file in c:\Sbparts\Custom
'Recent Updates by ShopBot:
'  11/15/12 - Routine no longer tries to swap between measurement systems. If Setup (under 'T'ools in ShopBot)
'             has configured my_variable.sbc file for inches, ZZERO will expect inches; IF mm then mm; *will attempt to spot error!
'  11/15/12 - Now uses system SafeZ for pull-up (that is set in [VC])
'  8/20/13  - Adds speed downshift for XY to reduce packet size to allow a STOP on plunge
'  3/12/14  - Includes new tests for Prox Switch on Z-axis


'**TEST YOUR PLATE BY TAPPING IT AGAINST BIT --> SWITCH INDICATOR SHOULD ACTIVATE ON SCREEN

'Initialize
   SA,          'Let's be in Absolute Mode
   SF,0         'Turn off limit checking temporarily here

'Confirm Spindles OFF
   SO, 1, 0
   SO, 2, 0

'Determine whether we are in Preview mode or Move/Cut mode
   &modenow = %(22)
   IF &modenow = 1 then GoTo CHANGEMODE

'Load variables from my_variables.sbc, process called by Custom Cut 90
   C#,90
   VN, 0                                         'Deactivate the limit switch functionality on input #3

'INDIRECTLY - Make sure that the units are those used with the Set-Up Program and expected by my_variables.sbc
   &zbot = &my_ZzeroThickness
   GoSub CheckPlateThickness                     'No longer trying to adjust for incorrect units; just quit IF plate seems incorrect

   'Set thickness and pull-up distance
	  	&zup = %(29)
		if &zup = "" Then &zup = 0
		IF &zup < &zbot  then &zup = &Zbot * 2   'If pull up not greater than thickness, just double thickness

   'Set a value for our first A move
        &big_plunge = %(4) - 10
        IF %(25) = 1 then &big_plunge = %(4) - 254
	    &MoveZ = .125                            'For re-positioning after Z prox touch-off
		IF %(25) = 1 then &MoveZ = 3

   'Check to see if we should move to a zeroing location
        IF &my_ZzeroStartX <> no then GoSub STARTMOVE

   'Save current speeds and set zeroing speed
      &start_Zmove_Speed = %(74)
	  &start_XYmove_Speed = %(71)
      &new_Zmove_Speed = .25
	  &new_XYmove_Speed = .25
      IF %(25) = 1 then &new_Zmove_Speed = 6
      IF %(25) = 1 then &new_XYmove_Speed = 6

'----------------------------------------------------------------Zeroing PROGRAM 
FIRST PLUNGE:
'Make First Plunge... the following line will display first and wait for key stroke

'            *** Hit ENTER When above plate and Ready to Zero ***
  PAUSE        
  GOSUB TEST_CLEAR           'Test to make sure the plate is not already contacting or probe hooked up           
               
'Now set-up the contact switch action ... 
  ON INP(&my_ZzeroInput,1) GoTo CONTACT1 'this is where we'll go on contact with plate

  VS, &new_XYmove_Speed,, (&new_Zmove_Speed * 2 ) 'Set XY speed to reduce packet size and distance to STOP
  MA, &big_plunge            'Start continuous plunge ... while looking for contact to occur

'If we don't contact anything for some reason we end up here
  ON INP(&my_ZzeroInput,1)   'Turn off switch check
  VS, &start_XYmove_Speed,, &start_Zmove_Speed    'Reset Speeds because zeroing failed
 'Zeroing Failed!
  PAUSE
END                          'This END statement causes the program to end here IF no contact

SECOND PLUNGE:
'Make a Second Slower Plunge to get it really accurate
  GoSub TEST_CLEAR 'Check for clear again          
	         
'Set-up the second contact switch action ... 
  ON INP(&my_ZzeroInput,1) GoTo CONTACT2 'this is where we'll go on contact with plate
  VS,, , &new_Zmove_Speed
  &little_plunge = %(4) - (&Zbot + &zup)
  MA, &little_plunge         'Start continuous plunge ... looking for second contact to occur

'If we don't contact anything for some reason we end up here
  ON INP(&my_ZzeroInput,1)   'Turn off switch check
  VS, &start_XYmove_Speed,, &start_Zmove_Speed    'Reset Speeds because zeroing failed
 'Zeroing Failed!
  PAUSE
END                          'This END statement causes the program to end here IF no contact

'================================================================Subroutines

CONTACT1:
'This is our subroutine for action on hitting the plate the first time
        &first_Z_touch = %(4)  'Just a diagnostic variable; touch location can be checked with [UL]
     VA,,,,&zbot,,,,,0          'Set the approximate Z location based on thickness
     PAUSE 1    
     JA, &zup                  'Pull-up SLIGHTLY ... 
    'Now We're Ready to Make Slow Move for Accurate Reading
     PAUSE 1
     GoTo SECOND PLUNGE

CONTACT2:
'This is our subroutine for action on hitting the plate the second time
        &second_Z_touch = %(4) 'Just a diagnostic variable that can be checked with [UL]
     VA,,,,&zbot,,,,,0          'Set final Z location AND Zero Table Base Coordinate
     PAUSE 1
     VS, &start_XYmove_Speed,, &start_Zmove_Speed
     JA, &zup                  'Pull-up to safe height
	 ''IF &my_hasZProx = 1 then Gosub Has_ZProx
    '***ZEROED! REMEMBER TO REMOVE ALLIGATOR CLIP, AND PUT AWAY PLATE!***
     PAUSE
END '*** NORMAL END HERE

TEST_CLEAR:
     &cleared = &my_ZzeroInput + 50
     IF %(&cleared) = 1 then PAUSE 3
     IF %(&cleared) = 1 then GoTo NO_CLEAR
     RETURN

NO_CLEAR:
    'Reset the orginal Z speed
     VS, &start_XYmove_Speed,, &start_Zmove_Speed
    'Z-contact did not clear. Check the location of alligator clip and plate. If cutting metal, try placing a sheet of paper between the material and the plate. Exiting Zzero!
     PAUSE
END

STARTMOVE:
	 IF &AllReadyThere = 1 then RETURN       'This variable produced in MTC routine if user offset used
    'Hit OK to move to your preferred starting point to start the routine.
     PAUSE
     IF %(4) < &zup then JA, &zup
     J2, &my_ZzeroStartX, &my_ZzeroStartY
     RETURN

CheckPlateThickness:
	IF %(25) = 1 then goto MT
	IF %(25) = 0 then goto IT
	MT:
		IF &Zbot < 1 then Gosub PlateIncorrect
		RETURN
	IT:
		IF &Zbot > 2 then Gosub PlateIncorrect
		RETURN

PlateIncorrect:
	'Plate thickness may be incorrect do to selected units. Use Shopbot Setup under tools to correct plate thickness or units for zeroing.
	 PAUSE
END

CHANGEMODE:
    'Tool won't home in Preview Mode. Quit and change Move/Cut mode ...
     PAUSE 
END

	