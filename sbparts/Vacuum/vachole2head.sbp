'ShopBot Plenum File
'   Copyright 2005. ShopBot Tools, Inc.
'   GB 10/25/05
'User takes responsibility for the effects and outcomes of the use of this program.
'
C# 90
&XCENT=%(1)-&my_XinHead2offset
&YCENT=%(2)-&my_YinHead2offset 
HOLE:
INPUT "We are about to bore a vacuum port through the plenum and support board.  Check to be sure a 2 inch hole will not interfere with your cross supports.  Does the hole need to be relocated? Y/N" &ANS

IF &ANS="Y" THEN GOTO MOVEHOLE
IF &ANS="N" THEN GOTO CUTHOLE

MOVEHOLE:
' You elected to move the hole location.  When you hit enter, the keypad command will come up.  Use the arrow keys to move to a better hole center, Esc when ready.
PAUSE
SK
INPUT "Are you sure this is a safe place to bore a 2 inch hole? Y/N" &ANS1

&XCENT=%(1)-&my_XinHead2offset 
&YCENT=%(2)-&my_YinHead2offset 
IF &ANS1="N" THEN GOTO MOVEHOLE

CUTHOLE:
' The router will start and cutting will begin when you hit enter.
PAUSE
SO,1,1
PAUSE 2
&REPS=6
CP, 1.5, &XCENT, &YCENT, T,  1,  0,  360, -0.25,  &REPS,  1,  1,  4, 0, 1  
JZ,.25
J2,&XCENT,&YCENT
SO,1,0

DEPTHCHECK:
INPUT "Did your hole cut all the way through? Y/N" &ANS2

IF &ANS2="N" THEN GOTO DEEPERHOLE
END

DEEPERHOLE:
&REPS=&REPS+1
' We will now cut the hole .25 deeper.  Enter to continue.
PAUSE
SO,1,1
PAUSE 2
CP, 1.5, &XCENT, &YCENT, T,  1,  0,  360, -0.25,  &REPS,  1,  1,  4, 0, 1 
SO,1,0
JZ,.25
J2,&XCENT,&YCENT
GOTO DEPTHCHECK