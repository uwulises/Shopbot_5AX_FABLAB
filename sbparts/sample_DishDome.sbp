'Dish/Dome Routine -- For Quick Dishing or Doming an Area
'3/28/09th
'================================================================
'Initialize Values (set these for desired parameters)
  &Radius = 5
  &Size = 5          'Flatter dish uses Size smaller than curvature radius
  &Stepover = .0125  '10% of 1/8" ballnose
  &FullDepth = -.75  'This is overall Depth; Dome with a Positive Number
  &PropX = 1         'Use for Elipse (for smooth surface, only go smaller than 1)
  &PropY = 1
  &PropShell = 1     'Create a Shell Effect with values from 0 to 1 by offsetting center

  'Derived Starting Values
     &StartX = %(1)                                'Get start locations
	 &StartY = %(2)
	 &StartZ = %(3)
	 &Y_Loc = &StartY
     &DepthProp = &FullDepth / &Size
     &dec_SizeZ = SQR(&Radius ^ 2 - &Size ^ 2)     'Used to adjust Shallow Dish

     IF &FullDepth > 0 Then GOTO ConfigDome        'Its a dome if positive depth

    ConfigDish:
     &radial_Loc = &Size                           'Start dishing from outside
	 &Stepover = &Stepover * -1
	 GOTO CutLoop
	ConfigDome:
     &radial_Loc = 2*&Stepover                       'Start doming from inside
	 &dec_SizeZ = &dec_SizeZ * -1
     GOTO CutLoop

     
   CutLoop:
      &cur_Depth = &DepthProp * (SQR(&Radius ^ 2 - &radial_Loc ^ 2) - &dec_SizeZ)
	  M3, &StartX ,&Y_Loc, &cur_Depth
	  CC, &radial_Loc, T, 1,,,, 1, &PropX, &PropY, 0, 1, 0
	  &radial_Loc = &radial_Loc + &Stepover
      &Y_Loc = &Y_Loc + (&Stepover * .5 * &PropY * &PropShell)
	  If &radial_Loc > &Radius then GOTO Finish
    IF &radial_Loc > 0 Then GOTO CutLoop

   Finish:
    JZ, &StartZ
    END

